open js
open solid
open base_checkbox
open state
open db


type db_client_props =
    {
        ServerType : server_type
        ClientType : client_type
    }

let db_client (props : mut db_client_props) =
    inl db_connection_options, set_db_connection_options = use_db_connection_options ()

    inl get_locals () : array any = ;[
        !!("props.ServerType:", props.ServerType |> to_string)
        !!("props.ClientType:", props.ClientType |> to_string)
    ]
    inl log = use_log get_locals "#666"
    log Debug ;[!!"> db_client ()"]

    /<base_checkbox {
        Checked = fun () =>
            db_connection_options ()
            |> am.exists fun connection =>
                connection.server_type = props.ServerType
                && (connection.clients |> am.exists ((=) props.ClientType))
        OnChange = fun checked =>
            set_db_connection_options <|
                db_connection_options ()
                |> am.map fun connection =>
                    if connection.server_type = props.ServerType then
                        { connection with
                            clients =
                                if checked
                                then connection.clients /@ a ;[props.ClientType]
                                else connection.clients |> am.filter ((<>) props.ClientType)
                        }
                    else connection
        Props = fun props' => { props' with
            Id =
                inl client =
                    match props.ClientType with
                    | SignalRClient => "signalr"
                    | GunRsClient => "gunrs"
                    | GunJsClient => "gunjs"
                    | Memory => "mem"
                inl server =
                    match props.ServerType with
                    | SignalRServer => "signalr"
                    | GunRsServer => "gunrs"
                    | GunJsServer => "gunjs"
                Some $"$\"db-{!client}-{!server}\""
            Children = Some <| fun () => ;[
                props.ClientType |> to_string |> str
            ]
        }
    }
