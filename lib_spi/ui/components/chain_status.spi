open solid
open row
open base_button
open base_table
open real_util
open real_js
open base_icon
open hooks.use_fetch
open hooks.use_store


let chain_status (props : mut solid_props) =
    inl state, dispatch = use_store ()

    inl request, clear =
        use_fetch
            "profile/tmp/chain_status"
            state.profile.tmp.chain_status
            fun new_state state new_value =>
                new_state.profile.tmp.chain_status <- new_value
            fun client => promise ($"!client?status()?do()" : any)

    create_custom_component row {
        Status = CheckHidden
        OnLoad = None
        OnUnload = None
        Props = fun props' => {props' with
            Id = Some <| "status"
            Title = Some <|
                stack
                    fun props' => {props' with
                        AlignItems = Some "start"
                        Direction = Some <| breakpoints {initial = "row"; sm = "column"}
                        MarginBottom = Some <| breakpoints {initial = "0"; sm = "8px"}
                        Spacing = Some "7px"
                        Padding = Some "3px"
                    }
                    fun () => ;[
                        box
                            id
                            fun () => ;[
                                str "Chain Status"
                            ]
                        create_component base_button
                            fun props' => {props' with
                                LeftIcon = Some <|
                                    create_custom_component base_icon {
                                        Icon = biRegularPlus_
                                        Props = id
                                        Css = fun props' => {props' with
                                            MarginLeft = None
                                        }
                                    }
                                PaddingRight = Some "10px"
                                OnClick = Some request
                            }
                            fun () => ;[
                                str "Request"
                            ]
                        create_component base_button
                            fun props' => {props' with
                                LeftIcon = Some <|
                                    create_custom_component base_icon {
                                        Icon = biRegularMinus_
                                        Props = id
                                        Css = fun props' => {props' with
                                            MarginLeft = None
                                        }
                                    }
                                PaddingRight = Some "10px"
                                OnClick = Some clear
                            }
                            fun () => ;[
                                str "Clear"
                            ]
                    ]
            Children = Some fun () => ;[
                create_component base_table
                    id
                    fun () => ;[
                        index' {
                            Each' = state.profile.tmp.chain_status |> am.toList |> listm.rev |> listm.toArray
                        } fun item => ;[
                            create_custom_component row {
                                Status = CheckHidden
                                OnLoad = None
                                OnUnload = None
                                Props = fun props' => {props' with
                                    Title = Some <| str (item () |> fst |> real to_string `i32)
                                    Padding = Some "3px"
                                    Children = Some fun () => ;[
                                        pre
                                            id
                                            fun () => ;[
                                                item ()
                                                |> snd
                                                |> real stringify_fmt `any
                                                |> str
                                            ]
                                    ]
                                }
                            }
                        ]
                    ]
            ]
        }
    }
