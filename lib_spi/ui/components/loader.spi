open solid
open state
open base_button


let loader (props : list prop_) =
    inl state, dispatch, state_obj, dispatch_obj = use_store ()

    inl loader_id = random ()

    inl loaded, set_loaded = create_signal (props /. .Loaded__ /? false)
    inl refreshing, set_refreshing = create_signal false
    inl modal, set_modal = create_signal (props /. .Modal__ /? false)

    on
        fun () => ;[refreshing ()]
        fun () => if refreshing () then set_refreshing false
    |> create_effect

    inl on_load' () =
        match props /. .OnLoad__ with
        | Some on_load => on_load state_obj dispatch_obj
        | None => ()

    if props /. .Loaded__ = Some true then on_load' ()

    inl icon_height = "20px"
    inl button_size = "14px"

    box <| (
        match props /. .Id with
        | Some id => [
        Id id
        ] | _ => []) /@ [
        Display "flex"
        Flex "1"
        Children fun () => ;[
            if loaded () |> not then
                box [
                    Padding "4px 0"
                    Children fun () => ;[
                        create_component base_button [
                            OnClick fun () =>
                                on_load' ()
                                set_loaded true
                            Children fun () => ;[
                                str "Load"
                            ]
                        ]
                    ]
                ]
            else
                box <|
                    (if modal () then [
                    ZIndex 1
                    Position "absolute"
                    Top "0"
                    Right "0"
                    Bottom "0"
                    Left "0"
                    BackgroundColor "$bg"
                    ] else []) /@ [
                    Display "flex"
                    Flex "1"
                    Children fun () => ;[
                        match props /. .Children with
                        | Some children =>
                            box <|
                                (if state.ui.modal () <= 0 then [
                                Position "relative"
                                ] else []) /@ [
                                Flex "1"
                                Display "flex"
                                Children fun () => ;[
                                    stack <|
                                        (match state.ui.modal () with
                                        | x when x > 0 && x <> loader_id => [
                                            Display "none"
                                        ] | _ => []) /@ [
                                        Direction "row"
                                        Spacing "3px"
                                        Position "absolute"
                                        Top "6px"
                                        Right "6px"
                                        ZIndex 1
                                        Children fun () => ;[
                                            icon_button [
                                                AriaLabel "Refresh"
                                                Size "xs"
                                                Height icon_height
                                                ColorScheme "neutral"
                                                OnClick fun () => set_refreshing true
                                                Icon <| biRegularRefresh [
                                                    Size button_size
                                                ]
                                            ]
                                            icon_button [
                                                AriaLabel (if modal () then "Restore" else "Maximize")
                                                Size "xs"
                                                Height icon_height
                                                ColorScheme "neutral"
                                                OnClick fun () =>
                                                    inl new_modal = not (modal ())
                                                    set_modal new_modal
                                                    inl new_state = {
                                                        ui = {
                                                            modal = if new_modal then Some loader_id else Some -3
                                                        }
                                                    }
                                                    dispatch new_state
                                                Icon <|
                                                    (if modal ()
                                                     then biRegularDownArrow
                                                     else biRegularUpArrow) [
                                                        Size button_size
                                                    ]
                                            ]
                                            icon_button [
                                                AriaLabel "Unload"
                                                Size "xs"
                                                Height icon_height
                                                ColorScheme "neutral"
                                                OnClick fun () => set_loaded false
                                                Icon <| biRegularUndo [
                                                    Size button_size
                                                ]
                                            ]
                                        ]
                                    ]
                                    if refreshing ()
                                    then empty_fragment ()
                                    else children_fragment children
                                ]
                            ]
                        | None =>
                            empty_fragment ()
                    ]
                ]
        ]
    ]
