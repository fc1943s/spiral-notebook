open real_solid
open solid
open state
open real_state
open js
open real_util
open base_button
open base_icon


type loader_props =
    {
        Loaded : bool
        Modal : bool
        OnLoad : option (() -> ())
        OnUnload : option (() -> ())
        Props : solid_props -> solid_props
    }

let loader (props : mut loader_props) =
    inl custom_props = props
    inl props = init_props props.Props

    inl loader_id = random ()

    inl chain_token, set_chain_token = use_chain_token ()
    inl log_level, set_log_level = use_settings_log_level ()
    inl ui_modal, set_ui_modal = use_settings_ui_modal ()

    inl get_locals () : array any = ;[
        !!("loader_id:",loader_id)
        !!("chain_token:", chain_token ())
        !!("log_level:", log_level () |> to_string)
        !!("ui_modal:", ui_modal ())
    ]
    inl log = use_log get_locals "#de884c"
    log Debug ;[!!"> loader ()"]

    inl loaded, set_loaded = create_signal custom_props.Loaded
    inl refreshing, set_refreshing = create_signal false

    create_effect fun () => if custom_props.Modal then set_ui_modal <| Some loader_id
    create_effect fun () => if refreshing () then set_refreshing false

    inl on_unload' () =
        match custom_props.OnUnload with
        | Some on_unload => on_unload ()
        | None => ()
    real on_cleanup on_unload'

    inl on_load' () =
        match custom_props.OnLoad with
        | Some on_load => on_load ()
        | None => ()

    if custom_props.Loaded = true then on_load' ()

    inl icon_height = "20px"

    inl is_modal () = ui_modal () = Some loader_id

    box
        fun props' => { props' with
            Id = props.Id
            Display = Some "flex"
            Flex = Some "1"
        }
        fun () => ;[
            if loaded () |> not then
                box
                    fun props' => { props' with
                        Padding = Some "3px 0"
                    }
                    fun () => ;[
                        /!!!base_button
                            fun props' => { props' with
                                OnClick = Some fun () =>
                                    on_load' ()
                                    set_loaded true
                            }
                            fun () => ;[
                                str "Load"
                            ]
                    ]
            else
                box
                    fun props' => { props' with
                        BackgroundColor = if is_modal () then Some "neutral.800" else None
                        Bottom = if is_modal () then Some "0" else None
                        Display = Some "flex"
                        Flex = Some "1"
                        Left = if is_modal () then Some "0" else None
                        Position = if is_modal () then Some "fixed" else None
                        Right = if is_modal () then Some "0" else None
                        Top = if is_modal () then Some "0" else None
                        ZIndex = if is_modal () then Some 1 else None
                    }
                    fun () => ;[
                        match props.Children with
                        | Some children =>
                            box
                                fun props' => { props' with
                                    Display = Some "flex"
                                    Flex = Some "1"
                                    Position =
                                        if ui_modal () = None
                                        then Some "relative"
                                        else None
                                }
                                fun () => ;[
                                    stack
                                        fun props' => { props' with
                                            Direction = Some "row"
                                            Display =
                                                match ui_modal () with
                                                | Some modal when modal <> loader_id => Some "none"
                                                | _ => None
                                            Position = Some "absolute"
                                            Right = Some "6px"
                                            Top = Some "6px"
                                            Spacing = Some "3px"
                                            ZIndex = Some 2
                                        }
                                        fun () => ;[
                                            icon_button
                                                fun props' => { props' with
                                                    AriaLabel = Some "Refresh"
                                                    ColorScheme = Some "neutral"
                                                    Height = Some icon_height
                                                    Icon = Some <|
                                                        /!!base_icon {
                                                            Xs = id
                                                            Icon = biRegularRefresh_
                                                            Props = id
                                                        }
                                                    OnClick = Some fun () => set_refreshing true
                                                    Size = Some "xs"
                                                }
                                                fun () => ;[]
                                            icon_button
                                                fun props' => { props' with
                                                    AriaLabel = Some <| if is_modal () then "Restore" else "Maximize"
                                                    ColorScheme = Some "neutral"
                                                    Height = Some icon_height
                                                    Icon = Some <|
                                                        /!!base_icon {
                                                            Xs = id
                                                            Icon =
                                                                if is_modal ()
                                                                then biRegularDownArrow_
                                                                else biRegularUpArrow_
                                                            Props = id
                                                        }
                                                    OnClick = Some fun () =>
                                                        set_ui_modal
                                                            if is_modal () |> not then Some loader_id else None
                                                    Size = Some "xs"
                                                }
                                                fun () => ;[]
                                            icon_button
                                                fun props' => { props' with
                                                    AriaLabel = Some "Unload"
                                                    ColorScheme = Some "neutral"
                                                    Height = Some icon_height
                                                    Icon = Some <|
                                                        /!!base_icon {
                                                            Xs = id
                                                            Icon = biRegularUndo_
                                                            Props = id
                                                        }
                                                    OnClick = Some fun () =>
                                                        on_unload' ()
                                                        set_loaded false
                                                    Size = Some "xs"
                                                }
                                                fun () => ;[]
                                        ]
                                    if refreshing ()
                                    then empty_fragment ()
                                    else children_fragment children
                                ]
                        | None => empty_fragment ()
                    ]
        ]
