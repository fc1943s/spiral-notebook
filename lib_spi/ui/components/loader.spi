open solid
open real_solid
open state
open store
open js
open real_js
open real_util
open base_button


type loader_props =
    {
        Loaded : bool
        Modal : bool
        OnLoad : option (mut state__ -> ((mut state__ -> mut state__ -> ()) -> ()) -> ())
        Props : mut_props
    }

inl loader (props : mut loader_props) =
    inl custom_props = props
    inl props = props.Props

    inl state, dispatch = use_store ()

    inl loader_id = real random ()

    inl loaded, set_loaded = create_signal' custom_props.Loaded
    inl refreshing, set_refreshing = create_signal' false
    inl modal, set_modal = create_signal' custom_props.Modal

    on'
        fun () => ;[real to_any `bool (refreshing ())]
        fun () => if refreshing () then set_refreshing false
    |> create_effect'

    inl on_load' () =
        match custom_props.OnLoad with
        | Some on_load => on_load state dispatch
        | None => ()

    if custom_props.Loaded = true then on_load' ()

    inl icon_height = "20px"
    inl button_size = "14px"

    box
        fun props' =>
            props'.Id <- props.Id
            props.Display <- Some "flex"
            props.Flex <- Some "1"
        fun () => ;[
            if loaded () |> not then
                box
                    fun props =>
                        props.Padding <- Some "3px 0"
                    fun () => ;[
                        create_component base_button
                            fun props =>
                                props.OnClick <- Some fun () =>
                                    on_load' ()
                                    set_loaded true
                            fun () => ;[
                                str "Load"
                            ]
                    ]
            else
                box
                    fun props =>
                        if modal () then
                            props.ZIndex <- Some 1
                            props.Position <- Some "absolute"
                            props.Top <- Some "0"
                            props.Right <- Some "0"
                            props.Bottom <- Some "0"
                            props.Left <- Some "0"
                            props.BackgroundColor <- Some "$bg"
                        props.Display <- Some "flex"
                        props.Flex <- Some "1"
                    fun () => ;[
                        match props.Children with
                        | None => empty_fragment ()
                        | Some children =>
                            box
                                fun props =>
                                    if state.ui.modal = None then
                                        props.Position <- Some "relative"
                                    props.Flex <- Some "1"
                                    props.Display <- Some "flex"
                                fun () => ;[
                                    stack
                                        fun props =>
                                            match state.ui.modal with
                                            | Some modal when modal <> loader_id =>
                                                props.Display <- Some "none"
                                            | _ => ()
                                            props.Direction <- Some "row"
                                            props.Spacing <- Some "3px"
                                            props.Position <- Some "absolute"
                                            props.Top <- Some "6px"
                                            props.Right <- Some "6px"
                                            props.ZIndex <- Some 1
                                        fun () => ;[
                                            icon_button
                                                fun props =>
                                                    props.AriaLabel <- Some "Refresh"
                                                    props.Size <- Some "xs"
                                                    props.Height <- Some icon_height
                                                    props.ColorScheme <- Some "neutral"
                                                    props.OnClick <- Some fun () => set_refreshing true
                                                    props.Icon <- Some <|
                                                        icon
                                                            fun props =>
                                                                props.As <- Some <| biRegularRefresh_ ()
                                                                props.Size <- Some button_size
                                                                props.Css <-
                                                                    props_entries fun props =>
                                                                        props.Zoom <- Some "0.6"
                                                                        props.MarginTop <- Some "-12px"
                                                                        props.MarginLeft <- Some "-12px"
                                                                    |> create_props |> Some
                                                            fun () => ;[]
                                                fun () => ;[]
                                            icon_button
                                                fun props =>
                                                    props.AriaLabel <- Some (if modal () then "Restore" else "Maximize")
                                                    props.Size <- Some "xs"
                                                    props.Height <- Some icon_height
                                                    props.ColorScheme <- Some "neutral"
                                                    props.OnClick <- Some fun () =>
                                                        inl new_modal = not (modal ())
                                                        set_modal new_modal
                                                        dispatch fun _old_state state =>
                                                            state.ui.modal <- if new_modal then Some loader_id else None
                                                    props.Icon <- Some <|
                                                        icon
                                                            fun props =>
                                                                props.As <- Some <|
                                                                    if modal ()
                                                                    then biRegularDownArrow_
                                                                    else biRegularUpArrow_
                                                                        ()
                                                                props.Size <- Some button_size
                                                                props.Css <-
                                                                    props_entries fun props =>
                                                                        props.Zoom <- Some "0.6"
                                                                        props.MarginTop <- Some "-12px"
                                                                        props.MarginLeft <- Some "-12px"
                                                                    |> create_props |> Some
                                                            fun () => ;[]
                                                fun () => ;[]
                                            icon_button
                                                fun props =>
                                                    props.AriaLabel <- Some "Unload"
                                                    props.Size <- Some "xs"
                                                    props.Height <- Some icon_height
                                                    props.ColorScheme <- Some "neutral"
                                                    props.OnClick <- Some fun () => set_loaded false
                                                    props.Icon <- Some <|
                                                        icon
                                                            fun props =>
                                                                props.As <- Some <| biRegularUndo_ ()
                                                                props.Size <- Some button_size
                                                                props.Css <-
                                                                    props_entries fun props =>
                                                                        props.Zoom <- Some "0.6"
                                                                        props.MarginTop <- Some "-12px"
                                                                        props.MarginLeft <- Some "-12px"
                                                                    |> create_props |> Some
                                                            fun () => ;[]
                                                fun () => ;[]
                                        ]
                                    if refreshing ()
                                    then empty_fragment ()
                                    else children_fragment children
                                ]
                    ]
        ]
